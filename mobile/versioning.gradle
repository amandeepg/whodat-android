ext {
    version="1.0.0-alpha3"
    buildVersionName = { version }
    buildVersionCode = {

        def candidate = "999"
        def (major, minor, patch) = version.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "")
        } else if (patch.contains("alpha")) {
            def alpha
            (patch, alpha) = patch.tokenize("alpha")
            if (alpha) {
                candidate = alpha
            }
        } else if (patch.contains("beta")) {
            def beta
            (patch, beta) = patch.tokenize("beta")
            if (beta) {
                candidate = 500 + beta.toInteger()
            }
        }

        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect { it.toInteger() }

        (major * 1_000_000) + (minor * 10_000) + (patch * 1000) + candidate;
    }
}
